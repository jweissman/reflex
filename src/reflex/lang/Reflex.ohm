Reflex {
    Program
      = delim* ListOf<Stmt, delim*> delim*

    Stmt = delim* Expr
      
    Expr =
      | CondExpr
	    | Defclass
      | Defun
      | LoopExpr
	    | CasualCall
	    | AlgExpr

    AlgExpr =
      | Message "=" Expr -- local_assign
      | BoolExpr

    BoolExpr
      = BoolExpr "||" Expr -- bool_or
      | BoolExpr "&&" Expr -- bool_and
      | CmpExpr "?" BoolExpr ":" BoolExpr -- tern
      | CmpExpr

    CmpExpr
      = CmpExpr "==" AddExpr -- eq
      | CmpExpr "!=" AddExpr -- neq
      | AddExpr

    AddExpr
      = AddExpr "+" MulExpr -- sum
      | AddExpr "-" MulExpr -- difference
      | MulExpr
    
    MulExpr
      = MulExpr "*" DotExpr -- product
      | MulExpr "/" DotExpr -- quotient
      | MulExpr "%" DotExpr -- modulo
      | DotExpr

    DotExpr
      = DotExpr "." Message "=" Expr -- sendEq
      | DotExpr "." Message ~lineTerminator Arguments -- call
      | DotExpr "." Message -- ivar_get
      | PriExpr

    PriExpr
      =
	    | "-" Expr -- neg
      | FunctionLit
      | ParenExpr -- parens
      | NumberLit
      | StringLit
	    | Bareword

    CasualCall
      = CasuallyCallableExpr ~lineTerminator Arguments

    CasuallyCallableExpr
      = Expr
      //| CasualCall
        
    ParenExpr
      = "(" Expr ")"

    LoopExpr
      = While Condition Block -- while 
      | Until Condition Block -- until 
    
    While = "while"
    Until = "until"
      
	  CondExpr =
      | CondParticle
	    | CondStmt
    CondTernary
      = Expr "?" Expr ":" Expr
      
    CondStmt
      = If Condition Then? CondBlock Else CondBlock -- ifThenElse
      | Unless Condition Then? CondBlock Else CondBlock -- unlessThenElse
      | If Condition Then? CondBlock -- ifThen
      | Unless Condition Then? CondBlock -- unlessThen

    CondParticle
     = Expr If Condition Else Expr  -- ifElse
     | Expr Unless Condition Else Expr -- unlessElse
     | Expr If Condition -- if
     | Expr Unless Condition -- unless

    CondBlock
      = Block
      | Expr

    Conj
      = If
      | Unless
    
    If
      = "if"
    
    Unless
      = "unless"
    Then
      = "then"
    Else
      = "else"
      | "otherwise"

    Condition 
      = ParenExpr
      | CmpExpr

    Defclass
      = Class ClassName ExtendsClass Block -- extends
      | Arch ClassName ExtendsClass Block -- arch_extends
      | Class ClassName Block -- plain
      | Arch ClassName Block -- arch_plain
    
    Arch
      = ~("A".."Z") ident
    
    Class
      = "class"

    ClassName
      = capitalIdent

    ExtendsClass
      = Extends Bareword

    Extends
      = "<"

    Defun
      = FunctionName FormalParams Block

    FunctionName
      = ident

    Message
      = ~Keyword ident
      
    Keyword = If | Else | Unless | Then | While | Until

    Bareword
      = Message
 
    Arguments
      = Args? PipedBlock -- block
      | Args -- no_block
   
    Args
      = FormalArguments
      | ~lineTerminator BareArguments
    
    FormalArguments
      = "(" ListOf<Arg, ","> ")"

    BareArguments
      = ~lineTerminator ~"(" NonemptyListOf<Arg, ",">
     
    Arg
      = "&" Expr -- ref
      | Expr
    
    Params
      = FormalParams
      | BareParams
      
    FormalParams
      = "(" ListOf<Param, ","> ")"
    
    BareParams
      = ~"(" NonemptyListOf<Param, ",">

    Param
      = "&" Bareword -- ref
      | Bareword

    Block 
      = "{" Program "}"
      
    PipedBlock
      = "{" PipeVars? Program "}"

    PipeVars
      = "|" ListOf<Param,","> "|"

    FunctionLit
      = FormalFunctionLiteral 
      | StabbyFunctionLiteral
    
    StabbyFunctionLiteral
      = "->" Block
      | "->" Expr
      
    FormalFunctionLiteral 
      = Params "=>" Block
      | Params "=>" Expr
    
    NumberLit
      = digit+ -- int
      | digit+ "." digit+ -- float
    
    StringLit
      = "'" #singleStringCharacter* "'"
      | "\"" #doubleStringCharacter* "\""

    singleStringCharacter
      = ~("\'" | "\\" | "\n") sourceCharacter -- non_escaped
    //   | "\\" unicodeLiteral                   -- escaped
   
    doubleStringCharacter
      = ~("\"" | "\\" | "\n") sourceCharacter -- non_escaped
    //   | "\\" unicodeLiteral                   -- escaped
    
    sourceCharacter = any
    
    // unicodeLiteral
    //   = "u" hexDigit hexDigit hexDigit hexDigit

    capitalIdent
      = "A".."Z" idChar*

    ident
      = letter idChar*
    
    idChar
      = alnum
      | "_"
    
    delim
      = delimiter
      | lineTerminator
      
    delimiter
      = ";"
    
    lineTerminator
      = "\n"
      | "\r"
  
    whitespace = #" " | " " | "\t"
    
    comment
      = shortComment
      | formalComment
    
    formalComment
      = formalCommentStart (~formalCommentEnd any)* formalCommentEnd
    formalCommentStart = "/*"
    formalCommentEnd = "*/"
   
    shortComment
      = commentMark #(~lineTerminator any)* lineTerminator?
    
	  commentMark
      = octothorpe
      | doubleSlash
    octothorpe
      = #"#"
    doubleSlash
      = #"/" #"/"
	  space
      := comment
      | whitespace
      | "\\" lineTerminator -- escapeNewline

}