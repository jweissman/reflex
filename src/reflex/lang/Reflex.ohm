Reflex {
    Program
      = delim* ListOf<Stmt, delim> delim*

    Stmt = delim* Expr
      
    Expr =
    	| LoopExpr
		  | CondExpr
	    | Defclass
      | Defun
      | CasualCall
		  | EqExpr
      | AlgExpr

     EqExpr =
       | ObjectDot "." Message "=" Expr -- send_eq
       | Message "=" Expr -- local_eq
       
     ObjectDot
       = ObjectDot "." Message CarefulArguments &"." -- call 
       | ObjectDot "." Message &"." -- dot
       | CoreExpr
    
    AlgExpr =
      | BoolExpr

    BoolExpr
      = 
      | BoolExpr "||" CmpExpr -- bool_or
      | BoolExpr "&&" CmpExpr -- bool_and
      | BoolExpr "?" CmpExpr ":" CmpExpr -- tern
      //| "!" CmpExpr -- bool_not
      | CmpExpr

    CmpExpr
      = CmpExpr "==" AddExpr -- eq
      | CmpExpr "!=" AddExpr -- neq
      | CmpExpr "<" AddExpr -- lt
      | CmpExpr ">" AddExpr -- gt
      | CmpExpr "<=" AddExpr -- lte
      | CmpExpr ">=" AddExpr -- gte
      | AddExpr

    AddExpr
      = AddExpr "+" MulExpr -- sum
      | AddExpr "-" MulExpr -- difference
      | MulExpr
    
    MulExpr
      = MulExpr "*" PriExpr -- product
      | MulExpr "/" PriExpr -- quotient
      | MulExpr "%" PriExpr -- modulo
      | PriExpr

    PriExpr
      =
	    | "!" PriExpr -- not
	    | "-" PriExpr -- neg
        | FunctionLit
        | ObjectExpr
      
     ObjectExpr
       = ObjectDot "." Message CarefulArguments -- call
       | ObjectDot "." Message -- dot
       | CoreExpr
       
     CoreExpr =
       | ParenExpr
       | NumberLit
       | StringLit
       | Message CarefulArguments -- funcall
       | Bareword

    CarefulCall
      = ObjectDot "." Message CarefulArguments
      
    CasualCall
      = ObjectDot "." Message CasualArguments -- obj
      | Bareword CasualArguments -- msg
      
    CarefulArguments
      = FormalArguments? PipedBlock -- block
      | FormalArguments
    
    CasualArguments
      = ~"-" BareArguments? PipedBlock -- block
      | ~"-" BareArguments

    FormalArguments
      = "(" ListOf<Arg, ","> ")"

    BareArguments
      = ~lineTerminator ~"(" NonemptyListOf<Arg, ",">
     
    Arg
      = "&" Expr -- ref
      | Expr
       
    Block 
      = "{" Program "}"
      
    PipedBlock
      = "{" PipeVars? Program "}"

    PipeVars
      = "|" ListOf<Param,","> "|"

    ParenExpr
      = "(" Expr ")"

    LoopExpr
      = While Condition Block -- while 
      | Until Condition Block -- until 
    
    While = "while"
    Until = "until"
      
	  CondExpr =
      | CondParticle
	    | CondStmt
    CondTernary
      = Expr "?" Expr ":" Expr
      
    CondStmt
      = If Condition Then? CondBlock Else CondBlock -- ifThenElse
      | Unless Condition Then? CondBlock Else CondBlock -- unlessThenElse
      | If Condition Then? CondBlock -- ifThen
      | Unless Condition Then? CondBlock -- unlessThen

    CondParticle
     = Expr If Condition Else Expr  -- ifElse
     | Expr Unless Condition Else Expr -- unlessElse
     | Expr If Condition -- if
     | Expr Unless Condition -- unless

    CondBlock
      = Block
      | Expr

    Conj
      = If
      | Unless
    
    If
      = "if"
    
    Unless
      = "unless"
    Then
      = "then"
    Else
      = lineTerminator Else -- newline
      | "else"
      | "otherwise"

    Condition 
      = ParenExpr
      | CmpExpr

    Defclass
      = Class ClassName ExtendsClass Block -- extends
      | Arch ClassName ExtendsClass Block -- arch_extends
      | Class ClassName Block -- plain
      | Arch ClassName Block -- arch_plain
    
    Arch
      = ~("A".."Z") ident
    
    Class
      = "class"

    ClassName
      = capitalIdent

    ExtendsClass
      = Extends Bareword

    Extends
      = "<"

    Defun
      = FunctionName FormalParams Block

    FunctionName
      = ident

    Message
      = ~Keyword ident
      
    Keyword = If | Else | Unless | Then | While | Until

    Bareword
      = Message
 
    Params
      = FormalParams
      | BareParams
      
    FormalParams
      = "(" ListOf<Param, ","> ")"
    
    BareParams
      = ~"(" NonemptyListOf<Param, ",">

    Param
      = "&" Bareword -- ref
      | Bareword

    FunctionLit
      = FormalFunctionLiteral 
      | StabbyFunctionLiteral
    
    StabbyFunctionLiteral
      = "->" Block
      | "->" Expr
      
    FormalFunctionLiteral 
      = Params "=>" Block
      | Params "=>" Expr
    
    NumberLit
      = digit+ "." digit+ -- float
      | #(~whitespace digit)+ -- int
    
    StringLit
      = "'" #singleStringCharacter* "'"
      | "\"" #doubleStringCharacter* "\""

    singleStringCharacter
      = ~("\'" | "\\" | "\n") sourceCharacter -- non_escaped
    //   | "\\" unicodeLiteral                   -- escaped
   
    doubleStringCharacter
      = ~("\"" | "\\" | "\n") sourceCharacter -- non_escaped
    //   | "\\" unicodeLiteral                   -- escaped
    
    sourceCharacter = any
    
    // unicodeLiteral
    //   = "u" hexDigit hexDigit hexDigit hexDigit

    capitalIdent
      = "A".."Z" idChar*

    ident
      = letter idChar*
    
    idChar
      = alnum
      | "_"
    
    delim
      = delimiter
      | lineTerminator
      
    delimiter
      = ";"
    
    lineTerminator
      = "\n"
      | "\r"
  
    whitespace = #" " | " " | "\t"
    
    comment
      = shortComment
      | formalComment
    
    formalComment
      = formalCommentStart (~formalCommentEnd any)* formalCommentEnd
    formalCommentStart = "/*"
    formalCommentEnd = "*/"
   
    shortComment
      = commentMark #(~lineTerminator any)* lineTerminator?
    
	  commentMark
      = octothorpe
      | doubleSlash
    octothorpe
      = #"#"
    doubleSlash
      = #"/" #"/"
	  space
      := comment
      | whitespace
      | "\\" lineTerminator -- escapeNewline

}