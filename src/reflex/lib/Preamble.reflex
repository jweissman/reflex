/*
 * class Object
 * The class of everything!
 */
class Object {
    neq(other) { self.eq(other).negate() }
    isA(klass) { self.isInstanceOf(klass) }
    nil() { false }
    true() { true }
    false() { Boolean.negate(self.true()) }
    toString() { self.inspect() }
}

/*
 * Class
 *
 * The class of classes.
 */
class Class {
    isDescendantOf(other) { other.isAncestorOf(self) }
    isKindOf(other) { other == self || other.isAncestorOf(self) }
    auto(&b) { meta.instanceEval(b) }
    include(mod) { mod.inject(self) }
    // call(...args) { new(args)
    // meta.instanceEval { call}
}

/**
 * Function
 *
 * The class of functions.
 */
class Function {
    meta.instanceEval { new(fn) { fn } }
    call() { self() }
}

/* 
 * Nihil
 *
 * The class of uninhabited types.
 */
class Nihil {
    nil() { true }
    true() { false }
}

/*
 * Boolean
 * 
 * The class of truth-values.
 */
class Boolean {
    eq(other) { self.isInstanceOf(other.class) }
    negate() { Boolean.negate(self) }
    meta.instanceEval {
        negate(value) {
            if (value.true()) { false } else { true }
        }
    }
}

class Truth < Boolean {
    negate() { false }
}

class Falsity < Boolean {
    true() { false }
    negate() { true }
}

/*
 * Number
 * 
 * The class of numeric values
 */
class Number {
    zero() { self == 0 }
    one() { self == 1 }
    positive() { self.gt(0) }
    true() { self.zero().negate() }
    divide(other) {
        if (other.zero()) {
            if (self.zero()) {
                Indeterminate.new()
            } else {
                if (self.positive()) {
                    PositiveApeiron.new()
                } else {
                    NegativeApeiron.new()
                }
            }
        } else {
            self.rawDiv(other)
        }
    }

    times(&timesBlock) {
        counter = self;
        until (counter.zero()) {
            counter = counter - 1;
            timesBlock()
        }
    }

    upto(stop,&block) {
        r=Range.new(self, stop)
        r.each(&block) if block_given
        r
    }
}

class Integer < Number {}
class Float < Number {}
class Indeterminate < Number {}
class Apeiron < Number {
    add(other) { self }
    subtract(other) { self }
    multiply(other) { self }
    divide(other) { self }
}
class PositiveApeiron < Apeiron { negate() { NegativeApeiron.new() } }
class NegativeApeiron < Apeiron { negate() { PositiveApeiron.new() } }

class Iterator {}

class ArrayIterator < Iterator {
    init(arr) {
        self.array = arr
        self.index = 0;
        self.indicate = false;
        self.exhausted = false;
        // self.mapFn = i=>i
    }

    advance() {
        self.index = self.index + 1;
        if (self.index >= self.array.length()) {
            self.exhausted = true;
        }
    }

    next() {
        if (!self.exhausted) {
            val=self.array.get(self.index)
            self.advance()
            val
        } else {
        //   nil
            fail 'stop iteration'
        }
    }
}

id = z=>z
class Enumerator {
    init(enumerable) {
        self.it = ArrayIterator.new(enumerable)
        // if (source.isA(Enumerable)) {
        // }
        //self.enumerable = enumerable;
        //self.index = 0;
        self.indicate = false;
        //self.exhausted = false;
        self.mapFn = id
    }
    map(&fn) {
       if (self.mapFn == id) {
          self.mapFn = fn
       } else {
           oldFn = self.mapFn
           self.mapFn = arg => fn(oldFn(arg))
       }
       self
    }
    withIndex(&b) {
        self.indicate = true;
        if (block_given) {
            self.call(&b)
        }
        self
    }

    each(&b) { if (block_given) { self.call(&b) } else { self } }
    next() {
        self.mapFn(self.it.next())
    }
    call(&b) {
        self.tilExhausted(&b)
    }
    collect() {
        arr=[]
        self.tilExhausted { |v| arr.push v }
        arr
    }
    toArray() { self.collect() }
    tilExhausted(&block) {
        until (self.it.exhausted) {
            if (self.indicate) {
                block(self.next(), self.it.index)
            } else {
                block(self.next())
            }
        }
    }
}

class Module {
    inject(target) { Kernel.include(self, target) }
}

module Enumerable {
    map(&mapFn) { self.each().map(&mapFn) }
    collect() { self.each().collect() }
    first() { self.collect().first() }
    rest() { self.collect().rest() }

    // 'hard eval' def which falls back to toArray
    each(&b) {
        if (block_given) { self.toArray().each(&b) }
        else { self.toArray().each() }
    }

    eachWithIndex(&b) {
        it = self.each().withIndex()
        if (block_given) { it(&b) }
        it
    }

    all() { self.collect()}

    join(delim) { self.collect().join(delim) }
}

/**
 * Array
 *
 * The class of object-valued index spaces.
 */
class Array {
    include Enumerable

    push(it) {
        self.set(self.length(), it)
        self
    }

    add(other) {
        self.concat(other)
    }

    eq(other) { 
        isEq = self.length() == other.length()
        if (isEq) {
            self.eachWithIndex { |x, i|
                if (x != other.get(i)) {
                    isEq = false 
                }
            }
        }
        isEq
    }

    toArray() { self }

    each(&b) {
        it = Enumerator.new(self)
        if (block_given) { it(&b) }
        it
    }
    
    split(sep, &blk) {
        groups = [];
        fragment = [];
        i = 0;
        delim = if block_given then blk else sep.eq 
        while (i <= self.length()-1) {
            e = self.get(i);
            if (delim(e)) {
                if (fragment.length() > 0) {
                    groups.push(fragment)
                    fragment=[]
                }
            } else {
                fragment.push(e)
            }
            i=i+1
        }
        if (fragment.length() > 0) {
            groups.push(fragment)
            fragment=[]
        }
        groups
    }

    first() { self.get(0) }
    rest() {
        rst=[]
        1.upto(self.length()-1) { |i| rst.push(self.get(i)) }
        rst
    }

    reverse() {
        if (self.length().zero()) { [] }
        else {
            self.rest().reverse() + [self.first()]
        }
    }

    inject(&fn) {
        res = self.first()
        apply = e => {
            partial = fn(res, e)
            res = partial
        }
        self.rest().each &apply
        res
    }

    join(delim) {
        if (self.length() > 1) {
          d=delim||''
          sum = (a,b) => { a+d+b }
          self.inject &sum
        } else { self.get(0) }
    }
}


/**
 * String
 *
 * The class of words
 */
class String {
    include Enumerable
    split(delim) {
        chars = self.toArray()
        if (delim.nil() || delim == '') {
            chars
        } else {
            groups = chars.split(delim)
            groups.map { |letters| letters.join() }.collect()
        }
    }

    toString() { self }
    //each(&b) {
    //    if (block_given) { self.toArray().each(&b) }
    //    else { self.toArray().each() }
    //}

    reverse() { self.toArray().reverse().join() }

    get(i) { self.toArray().get(i) }

    add(other) { self.concat(other) }
    multiply(other) {
        if (other.isInstanceOf(Number)) {
            res = ''
            other.times { res = res + self }
            res
        } else {
            fail "won't multiply string with non-numeric"
        }
    }
}

/**
 * Range
 * The class of bounded natural intervals.
 */
class Range {
    include Enumerable
    init(start,stop) { self.start=start; self.stop=stop; self.inc=1 }
    inspect() { start + ".." + stop }
    step(i) { self.inc=i; self }
    toArray() {
        arr = []
        i = self.start;
        while (i <= self.stop) {
           arr.push(i)
           i = i + self.inc
        }
        arr
    }
    call(&block) { self.each(&block) }
}

# the universal constants
nil = Nihil.new()
true = Truth.new()
false = Falsity.new()
Infinity = PositiveApeiron.new()
NegativeInfinity = NegativeApeiron.new()

using = Kernel.import
prints = Kernel.println
fail = Kernel.throw
//include = Kernel.include
noop = ->{}

// old ways die hard
require = Kernel.import
puts = o=>Kernel.println(o.toString())

// wire up main so it can define instance methods on itself...
self.defineMethod = meta.defineMethod