/*
 * class Object
 * The class of everything!
 */
class Object
    def neq other
      self.eq(other).negate()
    end

    isA(klass) { self.isInstanceOf(klass) }
    nil() { false }
    true() { true }
    false() { Boolean.negate(self.true()) }
    toString() { self.inspect() }
end

/*
 * Class
 *
 * The class of classes.
 */
class Class
    def isDescendantOf(other)
      other.isAncestorOf(self)
    end

    isKindOf(other) { other == self || other.isAncestorOf(self) }
    auto(&b) { meta.instanceEval(b) }
    include(mod) { mod.inject(self) }
end

/**
 * Function
 *
 * The class of functions.
 */
class Function {
    meta.instanceEval { new(fn) { fn } }
    call() { self() }
}

/* 
 * Nihil
 *
 * The class of uninhabited types.
 */
class Nihil {
    nil() { true }
    true() { false }
}

/*
 * Boolean
 * 
 * The class of truth-values.
 */
class Boolean {
    eq(other) { self.isInstanceOf(other.class) }
    negate() { Boolean.negate(self) }
    meta.instanceEval {
        negate(value) {
            if (value.true()) { false } else { true }
        }
    }
}

class Truth < Boolean {
    negate() { false }
}

class Falsity < Boolean {
    true() { false }
    negate() { true }
}

/*
 * Number
 * 
 * The class of numeric values
 */
class Number {
    zero() { self == 0 }
    one() { self == 1 }
    positive() { self.gt(0) }
    true() { self.zero().negate() }
    divide(other) {
        if (other.zero()) {
            if (self.zero()) {
                Indeterminate.new()
            } else {
                if (self.positive()) {
                    PositiveApeiron.new()
                } else {
                    NegativeApeiron.new()
                }
            }
        } else {
            self.rawDiv(other)
        }
    }

    times(&timesBlock) {
        it = RangeIterator.new(0.upto(self-1))
        if (block_given) {
            it(&timesBlock)
        }
        it
        //counter = self;
        //until (counter.zero()) {
        //    counter = counter - 1;
        //    timesBlock()
        //}
    }

    upto(stop,&block) {
        r=Range.new(self, stop)
        r.each(&block) if block_given
        r
    }
}

class Integer < Number {}
class Float < Number {}
class Indeterminate < Number {}
class Apeiron < Number {
    add(other) { self }
    subtract(other) { self }
    multiply(other) { self }
    divide(other) { self }
}
class PositiveApeiron < Apeiron { negate() { NegativeApeiron.new() } }
class NegativeApeiron < Apeiron { negate() { PositiveApeiron.new() } }

/// enumerability /////

class Module {
    inject(target) { Kernel.include(self, target) }
}

module Enumerable {
    map(&mapFn) { self.each().map(&mapFn) }
    collect() { self.each().collect() }
    first() { self.collect().first() }
    last() { self.collect().last() }
    rest() { self.collect().rest() }

    // 'hard eval' def which falls back to toArray...
    // with yield semantics this could be more efficient!
    each(&b) {
        if (block_given) { self.toArray().each(&b) }
        else { self.toArray().each() }
    }

    eachWithIndex(&b) {
        it = self.each().withIndex()
        if (block_given) { it(&b) }
        it
    }

    // all() { self.collect()}
    size() { self.collect().length() }
    sample() { self.collect().get(Kernel.rand(self.size()-1))}
    reverse() { self.collect().reverse() }
    includes(v) { self.collect().includes(v) }
    select(&b) { self.collect().select(&b) }
    detect(&b) { self.collect().detect(&b) }
    any(&b) { self.collect().any(&b) }

    join(delim) { self.collect().join(delim) }
}

class Iterator {
    // include Enumerable
    init() {
        self.index = 0;
        self.indicate = false;
        self.exhausted = false;
    }

    withIndex(&b) {
        self.indicate = true;
        if (block_given) {
            self.call(&b)
        }
        self
    }

    next() {
        if (!self.exhausted) {
            val=self.curr()
            self.advance()
            val
        } else {
            fail 'stop iteration'
        }
    }

    curr() { fail "Implement Iterator#curr in " + self.class }
    isExhausted() { self.exhausted }
    call(&block) {
        until (self.isExhausted()) {
            if (self.indicate) {
                block(self.next(), self.index)
            } else {
                block(self.next())
            }
        }
    }
    toArray() {
        arr=[]
        self { |i| arr.push(i) }
        arr
    }
}

class ArrayIterator < Iterator {
    init(arr) {
        self.array = arr
        // self.index = 0;
        // self.exhausted = false;
        super.init()
    }

    advance() {
        self.index = self.index + 1;
        if (self.index >= self.array.length()) {
            self.exhausted = true;
        }
    }

    curr() { self.array.get(self.index) }
}

class RangeIterator < Iterator {
    init(range) {
        super.init()
        self.range = range;
        self.index = range.start;
        // self.exhausted = false;
    }

    advance() {
        step = range.inc * range.dir
        self.index = self.index + step;
        if (range.dir == 1 && self.index > range.stop) {
            self.exhausted = true;
        }
        if (range.dir == -1 && self.index < range.stop) {
            self.exhausted = true;
        }
    }

    curr() { self.index }
}

class Enumerator {
    include Enumerable
    init(it) {
        self.it = it
        self.indicate = false;
    }

    map(&fn) {
       MappedEnumerator.new(self, fn)
    }

    withIndex(&b) {
        self.indicate = true;
        if (block_given) {
            self.call(&b)
        }
        self
    }

    each(&b) { if (block_given) { self.call(&b) }; self } // else { self } }
    next() {
        self.it.next()
    }
    call(&b) {
        self.tilExhausted(&b)
    }
    collect() {
        arr=[]
        self.tilExhausted { |v| arr.push v }
        arr
    }
    select(&b) {
        arr=[]
        self.tilExhausted { |v| arr.push(v) if b(v) }
        arr
    }
    any(&pred) {
        // puts "---> ANY MATCH " + pred
        matched = false
        // self.tilConditionOrExhausted(->matched) { |elem|
        until (self.isExhausted() || matched) {
            elem = self.next()
            matched = pred(elem)
            // puts "---> ELEM " + elem + " MATCH " + matched
        }
        matched
    }
    detect(&pred) {
        // puts "---> DETECT MATCH " + pred
        matched = false
        detected = nil
        // self.tilConditionOrExhausted(->matched) { |elem|
        until (self.isExhausted() || matched) {
            elem = self.next()
            matched = pred(elem)
            detected = elem
            // puts "---> ELEM " + elem + " MATCH " + matched
        }
        detected
    }
    toArray() { self.collect() }
    isExhausted() { self.it.exhausted }
    tilExhausted(&block) {
        until (self.isExhausted()) {
            if (self.indicate) {
                block(self.next(), self.it.index)
            } else {
                block(self.next())
            }
        }
    }
    // tilConditionOrExhausted(cond,&block) {
    //     val = self.next()
    //     until (cond() || self.isExhausted()) {
    //         val = self.next()
    //         if (self.indicate) {
    //             block(val, self.it.index)
    //         } else {
    //             block(val)
    //         }
    //     }
    // }
}

class MappedEnumerator < Enumerator {
    init(underylingEnum, mapFn) {
        self.underlyingEnum = underylingEnum
        self.mapFn = mapFn
        super.init(underylingEnum.it)
    }
    isExhausted() { self.underlyingEnum.isExhausted() }
    next() {
        self.mapFn(self.underlyingEnum.next())
    }
    inspect() { "MappedEnum["+mapFn+" over " +underlyingEnum.inspect() +"]"}
}


/**
 * Array
 *
 * The class of object-valued index spaces.
 */
class Array {
    include Enumerable

    push(item) {
        self.set(self.length(), item)
        self
    }

    add(other) {
        self.concat(other)
    }

    includes(item) {
        self.any { |e|
            // puts "Array.includes: test e " + e.inspect() + " == " + item.inspect()
            e == item
        }
    }

    // subtract(other) {
    //     self.select { |e| other.includes(e) }
    //     // self.subtract(other)
    // }

    eq(other) { 
        isEq = self.length() == other.length()
        if (isEq) {
            self.eachWithIndex { |x, i|
                if (x != other.get(i)) {
                    isEq = false 
                }
            }
        }
        isEq
    }

    toArray() { self }

    each(&b) {
        it = ArrayIterator.new(self)
        enum = Enumerator.new(it)
        if (block_given) { enum(&b) }
        enum
    }
    
    split(sep, &blk) {
        groups = [];
        fragment = [];
        i = 0;
        delim = if block_given then blk else sep.eq 
        while (i <= self.length()-1) {
            e = self.get(i);
            if (delim(e)) {
                if (fragment.length() > 0) {
                    groups.push(fragment)
                    fragment=[]
                }
            } else {
                fragment.push(e)
            }
            i=i+1
        }
        if (fragment.length() > 0) {
            groups.push(fragment)
            fragment=[]
        }
        groups
    }

    first() { self.get(0) }
    last() { self.get(self.length()-1) }
    rest() {
        rst=[]
        1.upto(self.length()-1) { |i| rst.push(self.get(i)) }
        rst
    }

    select(&b) { self.each().select(&b) }
    detect(&b) { self.each().detect(&b) }
    any(&b) { self.each().any(&b) }

    reverse() {
        len = self.length()
        rev=[]
        1.upto(len) { |i| rev.push(self.get(len-i)) }
        rev
        //if (self.length().zero()) { [] }
        //else {
        //    self.rest().reverse() + [self.first()]
        //}
    }

    inject(&fn) {
        res = self.first()
        apply = e => {
            partial = fn(res, e)
            res = partial
        }
        self.rest().each &apply
        res
    }

    join(delim) {
        if (self.length() > 1) {
          d=delim||''
          sum = (a,b) => { a+d+b }
          self.inject &sum
        } else { self.get(0) }
    }
}


/**
 * String
 *
 * The class of words
 */
class String {
    include Enumerable
    split(delim) {
        chars = self.toArray()
        if (delim.nil() || delim == '') {
            chars
        } else {
            groups = chars.split(delim)
            groups.map { |letters| letters.join() }.collect()
        }
    }

    toString() { self }
    get(i) { self.split('').get(i) }

    add(other) { self.concat(other) }
    reverse() { self.collect().reverse().join() }
    multiply(other) {
        if (other.isInstanceOf(Number)) {
            res = ''
            other.times { res = res + self }
            res
        } else {
            fail "won't multiply string with non-numeric"
        }
    }
}

/**
 * Range
 * The class of bounded natural intervals.
 */
class Range {
    include Enumerable
    init(start,stop) {
        self.start=start;
        self.stop=stop;
        self.inc=1;
        self.dir=self.start<self.stop?1:-1;
    }
    inspect() { start + ".." + stop }
    step(i) { self.inc=i; self }
    each(&b) {
        it = RangeIterator.new(self)
        enum = Enumerator.new(it)
        if (block_given) { enum(&b) }
        enum
    }
    
    toArray() { self.collect() }
    call(&block) { self.each(&block) }

    detect(&b) { self.each().detect(&b) }
    any(&b) { self.each().any(&b) }
}

/////

# the universal constants
nil = Nihil.new()
true = Truth.new()
false = Falsity.new()
Infinity = PositiveApeiron.new()
NegativeInfinity = NegativeApeiron.new()


using = Kernel.import
prints = Kernel.println
fail = Kernel.throw
//include = Kernel.include
noop = ->{}

// old ways die hard
require = Kernel.import
puts = o=>Kernel.println(o.toString())

// wire up main so it can define instance methods on itself...
self.defineMethod = meta.defineMethod